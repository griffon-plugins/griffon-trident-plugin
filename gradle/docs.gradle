javadoc {
    excludes = ['**/*.html', 'META-INF/**', '**/*.groovy']

    options.use         = true
    options.splitIndex  = true
    options.encoding    = 'UTF-8'
    options.author      = true
    options.version     = true
    options.windowTitle = "$project.name $project.version API"
    options.docTitle    = "$project.name $project.version API"
    options.footer      = javadocFooter
    options.links       = ['http://www.slf4j.org/apidocs/',
                           'http://junit.org/junit4/javadoc/latest/',
                           'http://docs.oracle.com/javase/8/docs/api/',
                           'http://static.javadoc.io/com.google.code.findbugs/jsr305/3.0.1/',
                           'http://static.javadoc.io/javax.inject/javax.inject/1/',
                           "http://griffon-framework.org/guide/${griffonVersion}/api".toString()]

    doLast { task ->
        copy {
            into task.destinationDir
            from project(':griffon-trident-guide').file('src/javadoc/resources/img/griffon.ico'),
                project(':griffon-trident-guide').file('src/javadoc/resources/css/stylesheet.css')
        }
        copy {
            into file("${task.destinationDir}/resources")
            from project(':griffon-trident-guide').file('src/javadoc/resources/img/')
        }
    }
}

if (plugins.hasPlugin('groovy')) {
    groovydoc {
        use = true
        source = project.sourceSets.main.allSource
        classpath = javadoc.classpath
        ext.windowtitle = "$project.name $project.version API"
        ext.doctitle = "$project.name $project.version API"
        header = "$project.name $project.version API"
        footer = project.javadocFooter
        includePrivate = false
        link 'http://www.slf4j.org/apidocs/', 'org.slf4j.'
        link 'http://docs.oracle.com/javase/8/docs/api/', 'java.', 'org.xml.', 'javax.', 'org.w3c.'
        link 'http://static.javadoc.io/com.google.code.findbugs/jsr305/3.0.1/', 'javax.annotation.'
        link "http://griffon-framework.org/guide/${griffonVersion}/api".toString(), 'griffon.', 'org.codehaus.griffon.'
        link 'http://junit.org/junit4/javadoc/latest/', 'org.junit.', 'junit.'
        link 'http://static.javadoc.io/javax.inject/javax.inject/1/', 'javax.inject.'

        doLast { task ->
            copy {
                into task.destinationDir
                from project(':griffon-trident-guide').file('src/javadoc/resources/img/griffon.ico')
                rename 'griffon.ico', 'groovy.ico'
            }
        }
    }
}
